import os
import shutil
from snakemake.utils import min_version
from snakemake.utils import validate


# Settings --------------------------------------------------------------------

min_version("6.3.0")

shell.executable("bash")


configfile: os.path.join(workflow.basedir, "..", ".tests", "integration", "config", "config.yaml")


# Validating samples in rules/common


workdir: config["workdir"]


# Input rules ------------------------------------------------------------------


include: "rules/common.smk"


rule all:
    input:
        "allele_calling/cgmlst/allele_profiles.tsv",
        "allele_calling/cgmlst/allele_statistics.tsv",
        "allele_calling/cgmlst/timestamps.tsv",
        "join_clusters/main/merged_db/sample_cluster_information.tsv",
        "join_clusters/main/merged_db/orphan_samples.tsv",
        "join_clusters/sub/merged_db/sample_cluster_information.tsv",
        "join_clusters/sub/merged_db/orphan_samples.tsv",


# Includes --------------------------------------------------------------------


include: "rules/allele_calling.smk"
include: "rules/join_clusters.smk"


# Workflow --------------------------------------------------------------------
## send notifications with onsuccess and onerror


onstart:
    print(f"\nYou are using GEUEBT-CHEWIE version: {version}")
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline started\n")


onsuccess:
    try:
        for logfile in os.listdir(".snakemake/log/"):
            shutil.move(os.path.join(".snakemake/log", logfile), "logs")
        shutil.rmtree(".snakemake", ignore_errors=True)
    except:
        # if not executing .snakemake from workdir,
        # the log file will be in execution directory
        # as far as I know, there is now way to access this form here
        pass
    print("\nWorkflow finished, no error")
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline succesfully finished\n")


onerror:
    print(
        f"\nAn error occured, please consider filing an bug report."
        f"\n"
        f"\nIssue tracker: https://github.com/NRW-GEUBT/geuebt-chewie/issues"
    )
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline stopped on error\n")
